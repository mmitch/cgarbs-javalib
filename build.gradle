apply plugin: 'java'
/* we are a Java project, obviously */
sourceCompatibility = '1.6'


apply plugin: 'jacoco'
/* generate Java code coverage reports */
jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
}


apply plugin: 'com.cinnober.gradle.semver-git'
/* deduce build version number from git tags
   source: https://github.com/cinnober/semver-git */
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.2.1'
	}
}


apply plugin: 'maven-publish'
/* publish directly to maven repositories */
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'de.cgarbs'
			artifactId = 'cgarbs-javalib'

			from components.java
		}
	}
}
publishing {
	repositories {
		maven {
			name "cgarbs.de local repo"
			url "file:/home/mitch/pub/www/maven2"
			/* sad panda: aparrently SFTP currently only works with passwords, not with pubkey */
		}
	}
}


repositories {
	jcenter()
	repositories {
		maven {
			name "cgarbs.de repo"
			url "http://www.cgarbs.de/maven2"
		}
	}
}
dependencies {
	compile 'com.googlecode.json-simple:json-simple:1.1.1'
	testCompile 'junit:junit:4.12', 'org.hamcrest:hamcrest-library:1.3', 'de.cgarbs:cgarbs-javalib-test:0.1.0'
}

 /***
 **** my own tasks
 ***/

task fixit(type: Exec) {
	description 'Fixes line breaks and indentation on empty lines.'

	commandLine 'tools/fixit.sh'
}

task publishDropbox(type: Copy, dependsOn: jar) {
	description 'Deploy current JAR to Dropbox.'

	from( jar.destinationDir ) {
		include jar.archiveName
		rename { jar.baseName + '.jar' }
	}
	into '/home/mitch/Dropbox/schnucki/knittr'
}

task checkl10n() {
	description 'Check all localizations for missing property file keys.'

	/* FIXME: all paths handcrafted, there should be a better way */
	finalizedBy(
		checkl10nSingle('src/main/resources/de/cgarbs/lib/exception'),
		checkl10nSingle('src/main/resources/de/cgarbs/lib/glue'),
		checkl10nSingle('src/demos/guesser')
		)
}

task version() {
     description 'Show version that would be published'

     println 'version=' + version
}

def checkl10nSingle(path) {
	description 'Check one localization for missing property file keys.'

	return tasks.create("checkl10n /${path}", Exec) {
		List args = [ 'tools/check-l10n.pl' ]
		fileTree(dir: path).include('*.properties').each {
			File file -> args.add(file as String)
		}

		commandLine args
	}
}